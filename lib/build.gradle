plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.dokka' version '1.4.20'
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'quevedo.soares.leandro.AndroidBLEMadeEasy'
                artifactId = 'library'
                version = android.defaultConfig.versionName
            }
        }
    }

    preBuild.finalizedBy(updateReadmeVersion)
}

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'

    /* Dokka */
    dokkaGfmPlugin("org.jetbrains.dokka:kotlin-as-java-plugin:1.4.20")

    /* AndroidX Contracts */
    implementation 'androidx.fragment:fragment-ktx:1.3.0'
    implementation 'androidx.activity:activity-ktx:1.2.0'

    /* Coroutines */
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}

/* Define a gradle task to update the README.md file */
task updateReadmeVersion {
    doLast {
        def version = android.defaultConfig.versionName
        println("Updating README.md version to $version")

        def input = "implementation 'com\\.github\\.LeandroSQ:android-ble-made-easy:(.*?)'"
        def output = "implementation 'com.github.LeandroSQ:android-ble-made-easy:$version'"

        def file = new File("./README.md")
        def fileContent = file.text
        file.withWriter { writer -> writer << fileContent.replaceAll(input, output) }
    }
}
