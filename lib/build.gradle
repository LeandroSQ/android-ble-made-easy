plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'quevedo.soares.leandro.AndroidBLEMadeEasy'
                artifactId = 'library'
                version = android.defaultConfig.versionName
            }
        }
    }

    preBuild.finalizedBy(updateReadmeVersion)
}

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'

    /* Dokka */
    implementation "org.jetbrains.dokka:dokka-core:$dokka_version"
    dokkaGfmPlugin "org.jetbrains.dokka:kotlin-as-java-plugin:$dokka_version"

    /* AndroidX Contracts */
    implementation 'androidx.fragment:fragment-ktx:1.4.1'
    implementation 'androidx.activity:activity-ktx:1.4.0'

    /* Google Services Api */
    implementation 'com.google.android.gms:play-services-location:20.0.0'

    /* Coroutines */
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.2'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

/* Define a gradle task to update the README.md file */
task updateReadmeVersion {
    doLast {
        def version = android.defaultConfig.versionName
        println("Updating README.md version to $version")

        def input = "implementation 'com\\.github\\.LeandroSQ:android-ble-made-easy:(.*?)'"
        def output = "implementation 'com.github.LeandroSQ:android-ble-made-easy:$version'"

        def file = new File("./README.md")
        if (!file.exists()) file = new File("./../README.md")

        def fileContent = file.text
        file.withWriter { writer -> writer << fileContent.replaceAll(input, output) }
    }
}
